plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.21'
}


tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "Maven Central"
        url = "https://repo1.maven.org/maven2"
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // MixinExtras for advanced mixin features
    // NeoForge 20.2+ includes MixinExtras by default, but we can override with a specific version if needed
    // Using the version that should be compatible with NeoForge 21.1.215
    implementation("io.github.llamalad7:mixinextras-neoforge:0.5.0")

    // Curios optional dependency
    if (project.hasProperty('runtime_itemlist_mod') && project.runtime_itemlist_mod == "curios") {
        implementation "curse.maven:curios-309927:6529130"
    } else {
        compileOnly "curse.maven:curios-309927:6529130"
    }
    
    if (project.hasProperty('runtime_itemlist_mod') && project.runtime_itemlist_mod == "guideme") {
        implementation "curse.maven:guide-me-1173950:7127444"
    } else {
        compileOnly "curse.maven:guide-me-1173950:7127444"
    }

    // Optional dependencies for JEI/REI/EMI
    
    // JEI dependency (Curseforge)
    if (project.hasProperty('runtime_itemlist_mod') && project.runtime_itemlist_mod == "jei") {
        localRuntime "curse.maven:jei-238222:6516057"
    } else {
        compileOnly "curse.maven:jei-238222:6516057"
    }
    
    // REI dependencies (Curseforge)
    if (project.hasProperty('runtime_itemlist_mod') && project.runtime_itemlist_mod == "rei") {
        localRuntime "curse.maven:roughly-enough-items-310111:6199140"
        localRuntime "curse.maven:architectury-api-419699:5786327"
        localRuntime "curse.maven:cloth-config-348521:5729127"
    } else {
        compileOnly "curse.maven:roughly-enough-items-310111:6199140"
        compileOnly "curse.maven:architectury-api-419699:5786327"
        compileOnly "curse.maven:cloth-config-348521:5729127"
    }
    
    // EMI dependency (Curseforge)
    if (project.hasProperty('runtime_itemlist_mod') && project.runtime_itemlist_mod == "emi") {
        localRuntime "curse.maven:emi-580555:6420931"
        // Adds JEI at runtime for the JEI compatibility built into EMI
        localRuntime "curse.maven:jei-238222:6516057"
    } else {
        compileOnly "curse.maven:emi-580555:6420931"
    }
    
    // Verify that the value of runtime_itemlist_mod is valid
    if (project.hasProperty('runtime_itemlist_mod') && 
        !(project.runtime_itemlist_mod in ["jei", "rei", "emi", "curios", "none"])) {
        throw new GradleException("Invalid value for runtime_itemlist_mod: " + project.runtime_itemlist_mod + 
                                  ". Allowed values: jei, rei, emi, curios, none")
    }
    
    // Verify that the value of include_compass_mods is valid
    if (project.hasProperty('include_compass_mods') && 
        !(project.include_compass_mods in ["true", "false"])) {
        throw new GradleException("Invalid value for include_compass_mods: " + project.include_compass_mods + 
                                  ". Allowed values: true, false")
    }
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
